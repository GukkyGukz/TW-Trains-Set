
spriteset(spriteset_emu200_emc, "gfx/200.png") {
    tmpl_vehicle_8_views(0, 0)
}

spriteset(spriteset_emu200_ep, "gfx/200.png") {
    tmpl_vehicle_8_views(1, 0)
}

spriteset(spriteset_emu200_em, "gfx/200.png") {
    tmpl_vehicle_8_views(2, 0)
}

spriteset(spriteset_emu200_em_flip, "gfx/200.png") {
    tmpl_vehicle_8_views(2, 1)
}

spriteset(spriteset_emu200_purchase, "gfx/200.png") {
    template_purchase_dualheaded(3, 127)
}

switch(FEAT_TRAINS, SELF, switch_emu200_mucar_pos, position_in_vehid_chain % 3) {
    0: spriteset_emu200_ep;
    1: spriteset_emu200_em;
    2: spriteset_emu200_emc;
    return CB_RESULT_NO_TEXT;
}

switch(FEAT_TRAINS, SELF, switch_emu200_gfx_pos, position_in_consist) {
    0: spriteset_emu200_emc;
    spriteset_emu200_em_flip;
}

switch(FEAT_TRAINS, SELF, switch_emu200_mucar, num_vehs_in_vehid_chain % 3) {
    1: switch_emu200_mucar_pos;
    spriteset_trmu_car_add;
}

/* --- Start/stop callback  --- */
switch(FEAT_TRAINS, SELF, switch_emu200_start_stop, num_vehs_in_consist) {
    3: return CB_RESULT_NO_TEXT;
    6: return CB_RESULT_NO_TEXT;
    9: return CB_RESULT_NO_TEXT;
    12: return CB_RESULT_NO_TEXT;
    15: return CB_RESULT_NO_TEXT;
    return string(STR_UNIT_ERROR);
}

/* --- Wagon attach callback  --- */
switch(FEAT_TRAINS, SELF, switch_emu200_attach_vehid, vehicle_type_id) {
    trmu_car: return CB_RESULT_ATTACH_ALLOW;
    return string(STR_MUCAR_ERROR);
}

/* Define the actual train */
item(FEAT_TRAINS, emu200) {
    property {
        name:                         string(STR_NAME_EMU200);
        introduction_date:            date(1987, 6, 1);
        model_life:                   VEHICLE_NEVER_EXPIRES;
        vehicle_life:                 33;
        reliability_decay:            18;

        cargo_capacity:               48;
        loading_speed:                7;
        cost_factor:                  60;
        running_cost_factor:          54;

        speed:                        121 km/h;
        track_type:                   NABE;
        power:                        310 kW;
        weight:                       49 ton;

        climates_available:           ALL_CLIMATES;
        ai_special_flag:              AI_FLAG_PASSENGER;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        misc_flags:                   bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes:     bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost:                   0;
        dual_headed:                  1;
        ai_engine_rank:               0;

        running_cost_base:            RUNNING_COST_ELECTRIC;
        engine_class:                 ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered:    visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 4, ENABLE_WAGON_POWER);

        tractive_effort_coefficient:  0.3;
        air_drag_coefficient:         0.07;
        bitmask_vehicle_info:         0;
        length: 8;
    }
    /*Graphics and Callbacks*/
      graphics { // graphics for engine
         additional_text:  string(STR_CONCAT_5S, 
                        string(STR_DESC_POWER_AC20),
                        string(STR_DESC_NARROWGAUGE),
                        string(STR_DESC_USAGE_PAX_EXP),
                        string(STR_TRLENGTH_5T,
                           string(STR_TRLENGTH_3C),
                           string(STR_TRLENGTH_6C),
                           string(STR_TRLENGTH_9C),
                           string(STR_TRLENGTH_12C),
                           string(STR_TRLENGTH_15C)
                         ),
                        string(STR_CONCAT_1C, 
                           string(STR_SUFFIX_TRA),
                        )
                  );
        cargo_capacity: return 48;
        default:                switch_emu200_gfx_pos;
            purchase:               spriteset_emu200_purchase;
        can_attach_wagon:             switch_emu200_attach_vehid;
        start_stop:           switch_emu200_start_stop;
    }
    /*Multiple Unit Car*/
      livery_override(trmu_car){
        default:                      switch_emu200_mucar;
        cargo_capacity:               48;
        loading_speed:                7;
        power: 310;
        weight: 49;
      }
}
