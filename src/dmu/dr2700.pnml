
spriteset(spriteset_dr2700_silver, "gfx/dr2700.png") {
    tmpl_vehicle_8_views(2, 0)
}

spriteset(spriteset_dr2750, "gfx/dr2700.png") {
    tmpl_vehicle_8_views(4, 0)
}

spriteset(spriteset_dr2700_silver_rear, "gfx/dr2700.png") {
    tmpl_vehicle_8_views(3, 0)
}

spriteset(spriteset_dr2700_silver_flip, "gfx/dr2700.png") {
    tmpl_vehicle_8_views(3, 1)
}

spriteset(spriteset_dr2700_yellow, "gfx/dr2700.png") {
    tmpl_vehicle_8_views(0, 0)
}

spriteset(spriteset_dr2700_yellow_rear, "gfx/dr2700.png") {
    tmpl_vehicle_8_views(1, 0)
}

spriteset(spriteset_dr2700_yellow_flip, "gfx/dr2700.png") {
    tmpl_vehicle_8_views(1, 1)
}

spriteset(spriteset_dr2700_silver_purchase, "gfx/dr2700.png") {
    template_purchase_dualheaded(0, 191)
}

spriteset(spriteset_dr2700_yellow_purchase, "gfx/dr2700.png") {
    template_purchase_dualheaded(0, 159)
}
/* --- Graphics callback  --- */


switch(FEAT_TRAINS, SELF, switch_dr2700_silver_end, position_in_vehid_chain_from_end) {
    0: spriteset_dr2700_silver;
    spriteset_dr2700_silver_rear;
}

switch(FEAT_TRAINS, SELF, switch_dr2700_yellow_end, position_in_vehid_chain_from_end) {
    0: spriteset_dr2700_yellow;
    spriteset_dr2700_yellow_rear;
}

switch(FEAT_TRAINS, SELF, switch_dr2700_silver_mucar_pos, position_in_vehid_chain % 3) {
    0: spriteset_dr2750;
    1: switch_dr2700_silver_end;
    2: spriteset_dr2700_silver;
    return CB_RESULT_NO_TEXT;
}

switch(FEAT_TRAINS, SELF, switch_dr2700_yellow_mucar_pos, position_in_vehid_chain % 3) {
    0: spriteset_dr2750;
    1: switch_dr2700_yellow_end;
    2: spriteset_dr2700_yellow;
    return CB_RESULT_NO_TEXT;
}

switch(FEAT_TRAINS, SELF, switch_dr2700_silver_pos, position_in_consist) {
    0: spriteset_dr2700_silver;
    spriteset_dr2700_silver_flip;
}

switch(FEAT_TRAINS, SELF, switch_dr2700_yellow_pos, position_in_consist) {
    0: spriteset_dr2700_yellow;
    spriteset_dr2700_yellow_flip;
}

switch(FEAT_TRAINS, SELF, switch_dr2700_gfx_yeardep_livery, build_year) {
    0..1979: switch_dr2700_silver_pos;
    1980..2013: switch_dr2700_yellow_pos;
    switch_dr2700_silver_pos;
}

switch(FEAT_TRAINS, SELF, switch_dr2700_mucar_yeardep_livery, build_year) {
    0..1979: switch_dr2700_silver_mucar_pos;
    1980..2013: switch_dr2700_yellow_mucar_pos;
    switch_dr2700_silver_mucar_pos;
}

switch(FEAT_TRAINS, SELF, switch_dr2700_livery, cargo_subtype) {
    1: switch_dr2700_silver_pos;
    2: switch_dr2700_yellow_pos;   
    switch_dr2700_gfx_yeardep_livery;
}

switch(FEAT_TRAINS, SELF, switch_dr2700_mucar_livery, cargo_subtype) {
    1: switch_dr2700_silver_mucar_pos;
    2: switch_dr2700_yellow_mucar_pos;   
    switch_dr2700_mucar_yeardep_livery;
}



switch(FEAT_TRAINS, SELF, switch_dr2700_subtype_text, cargo_subtype) {
    0: return string(STR_CONCAT_1F, string(STR_SUFFIX_YEARDEP)
                                 );
    1: return string(STR_CONCAT_2, string(STR_SUFFIX_TRA), 
                                  string(STR_CONCAT_1F, 
                                    string(STR_COLOUR_SILVER)
                                    )
                                 );
    2: return string(STR_CONCAT_2, string(STR_SUFFIX_TRA),
                                    string(STR_CONCAT_1F, 
                                     string(STR_COLOUR_YELLOW)
                                     )
                                 );
       return CB_RESULT_NO_TEXT;
    }


switch (FEAT_TRAINS, SELF, switch_dr2700_purchase, current_year) {
    0..1979: spriteset_dr2700_silver_purchase;
    1980..2013: spriteset_dr2700_yellow_purchase;
    spriteset_dr2700_silver_purchase;
    }

/* --- Wagon attach callback  --- */
switch(FEAT_TRAINS, SELF, switch_dr2700_attach_vehid, vehicle_type_id) {
    trmu_car: return CB_RESULT_ATTACH_ALLOW;
    return string(STR_MUCAR_ERROR);
}

/* Define the actual train */
item(FEAT_TRAINS, dr2700) {
    property {
        name:                         string(STR_NAME_DR2700);
        introduction_date:            date(1966, 1, 1);
        model_life:                   VEHICLE_NEVER_EXPIRES;
        vehicle_life:                 55;
        reliability_decay:            18;

        cargo_capacity:               60;
        loading_speed:                5;
        cost_factor:                  60;
        running_cost_factor:          54;

        speed:                        111 km/h;
        track_type:                   NABN;
        power:                        150 hp;
        weight:                       35 ton;

        climates_available:           ALL_CLIMATES;
        ai_special_flag:              AI_FLAG_PASSENGER;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        misc_flags:                   bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes:     bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost:                   0;
        dual_headed:                  1;
        ai_engine_rank:               0;

        running_cost_base:            RUNNING_COST_DIESEL;
        engine_class:                 ENGINE_CLASS_DIESEL;
        visual_effect_and_powered:    visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 2, ENABLE_WAGON_POWER);

        tractive_effort_coefficient:  0.3;
        air_drag_coefficient:         0.07;
        bitmask_vehicle_info:         0;
        length: 8;
    }
    /*Graphics and Callbacks*/
      graphics { // graphics for engine
         additional_text:  string(STR_CONCAT_5S, 
                        string(STR_DESC_POWER_DH),
                        string(STR_DESC_NARROWGAUGE),
                        string(STR_DESC_USAGE_PAX_EXP),
                        string(STR_CONCAT_1C, 
                           string(STR_SUFFIX_TRA),
                        )
                  );
        cargo_capacity: return 52;
        default:                switch_dr2700_livery;
            purchase:               switch_dr2700_purchase;
        can_attach_wagon:             switch_dr2700_attach_vehid;
        cargo_subtype_text: switch_dr2700_subtype_text;
    }
    /*Multiple Unit Car*/
      livery_override(trmu_car){
        default:                      switch_dr2700_mucar_livery;
        cargo_capacity:               60;
        loading_speed:                5;
        power: 112;
        weight: 35;
      }
}
