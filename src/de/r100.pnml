
spriteset(spriteset_r100_blue, "gfx/r100.png") {
    tmpl_vehicle_8_views(0, 0)
}

spriteset(spriteset_r100_orange, "gfx/r100.png") {
    tmpl_vehicle_8_views(1, 0)
}

//Spriteset flipped
spriteset(spriteset_r100_flip_blue, "gfx/r100.png") {
    tmpl_vehicle_8_views_2(0, 1)
}

spriteset(spriteset_r100_flip_orange, "gfx/r100.png") {
    tmpl_vehicle_8_views_2(1, 1)
}


//purchase

spriteset(spriteset_r100_blue_purchase, "gfx/r100.png") {
    tmpl_vehicle_single(3, 87, 16, -25, -8)
}

spriteset(spriteset_r100_orange_purchase, "gfx/r100.png") {
    tmpl_vehicle_single(4, 87, 16, -25, -8)
}



//flip switches
switch(FEAT_TRAINS, SELF, switch_r100_flip_blue, vehicle_is_flipped) {
    0: spriteset_r100_blue;
    1: spriteset_r100_flip_blue;
}

switch(FEAT_TRAINS, SELF, switch_r100_flip_orange, vehicle_is_flipped) {
    0: spriteset_r100_orange;
    1: spriteset_r100_flip_orange;
}


switch(FEAT_TRAINS, SELF, switch_r100_yeardep_livery, build_year) {
    0..1979: switch_r100_flip_blue;
    1980..9999: switch_r100_flip_orange;
    switch_r100_flip_orange;
}

switch(FEAT_TRAINS, SELF, switch_r100_livery, cargo_subtype) {
    1: switch_r100_flip_blue;
    2: switch_r100_flip_orange;
    switch_r100_yeardep_livery;
}



switch(FEAT_TRAINS, SELF, switch_r100_subtype_text, cargo_subtype) {
    0: return string(STR_SUFFIX_YEARDEP);
    1: return string(STR_CONCAT_2, string(STR_SUFFIX_TRA), 
                                  string(STR_CONCAT_1F, 
                                    string(STR_COLOUR_NVBLUE)
                                    )
                                 );
    2: return string(STR_CONCAT_2, string(STR_SUFFIX_TRA),
                                    string(STR_CONCAT_1F, 
                                     string(STR_COLOUR_ORANGE)
                                     )
                                 );
       return CB_RESULT_NO_TEXT;
    }


switch (FEAT_TRAINS, SELF, switch_r100_purchase, current_year) {
    0..1979: spriteset_r100_blue_purchase;
    spriteset_r100_orange_purchase;
    }


item (FEAT_TRAINS, r100) {

property {
        name:                         string(STR_NAME_R100);
        climates_available:           ALL_CLIMATES;
        introduction_date:            date(1969, 1, 1);
        model_life:                   VEHICLE_NEVER_EXPIRES;
        vehicle_life:                 60;
        reliability_decay:            20;
        cost_factor:                  45;
        running_cost_factor:          100; // Changed by callback
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        length:                       6;
        speed:                        101 km/h;
        misc_flags:                   bitmask(TRAIN_FLAG_2CC);
        refit_cost:                   0; //refit costs don't apply to subcargo display
        track_type:                   NABN; // from rail type table
        power:                        1650 hp; // Changed by CB
        running_cost_base:            RUNNING_COST_DIESEL;
        cargo_capacity:               1;
        cargo_allow_refit:            [PASS];
        dual_headed:                  0;
        weight:                       78 ton; // Total, changed by callback
        ai_engine_rank:               0; // not intended to be used by the ai
        engine_class:                 ENGINE_CLASS_DIESEL;
        extra_power_per_wagon:        0 kW;
        // 6/6 of weight on driving wheels, with a default friction coefficient of 0.3:
        tractive_effort_coefficient:  0.3 / 1.5; // changed by callback
        air_drag_coefficient:         0.06;
        // Overridden by callback to disable for non-powered wagons:
        visual_effect_and_powered:    visual_effect_and_powered(VISUAL_EFFECT_DIESEL, 2, DISABLE_WAGON_POWER);
        extra_weight_per_wagon:       0 ton;
        bitmask_vehicle_info:         0;
    }


  graphics { // graphics for engine
  additional_text:  string(STR_CONCAT_4S, 
                        string(STR_DESC_POWER_DE),
                        string(STR_DESC_NARROWGAUGE),
                        string(STR_DESC_USAGE_GP),
                        string(STR_CONCAT_1C, 
                           string(STR_SUFFIX_TRA)
                    )
                  );
  cargo_capacity: return 0;
  cargo_subtype_text: switch_r100_subtype_text;
  default: switch_r100_livery;
  purchase: switch_r100_purchase;
  }
}
